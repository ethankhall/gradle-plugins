import groovy.json.JsonSlurper

pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url 'https://dl.bintray.com/ethankhall/crom/' }
    }
    resolutionStrategy {
        eachPlugin { details ->
            def id = details.requested.id.id
            if (id == 'tech.crom.version-manager' && details.requested.version == 'LATEST') {
                def blob = new JsonSlurper().parse(new URL("http://api.crom.tech/api/v1/project/ethankhall/repo/version-manager-clients/version/latest"))
                println "Using the latest version of version-manger, which is ${blob.version}"
                details.useVersion(blob.version)
            }
        }
    }
}
include 'klint-plugin'
include 'base-plugins'
include 'dependency-script-compiler'
include 'dependency-script-api'
include 'dependency-script-plugin'
include 'dependency-script-idea-plugin'

project(':dependency-script-compiler').projectDir = new File(settingsDir, 'dependency-script/compiler')
project(':dependency-script-api').projectDir = new File(settingsDir, 'dependency-script/api')
project(':dependency-script-plugin').projectDir = new File(settingsDir, 'dependency-script/plugin')
project(':dependency-script-idea-plugin').projectDir = new File(settingsDir, 'dependency-script/idea-plugin')
