plugins {
    id "nebula.integtest" version "3.3.0"
}
apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: "java-gradle-plugin"
apply plugin: 'groovy'

apply from : "$rootDir/gradle/publishing.gradle"

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

configurations {
    compiler
}

dependencies {
    compile gradleApi()
    compile (project(':dependency-script-api')) {
        transitive = false
    }
    compile 'org.jetbrains.kotlin:kotlin-stdlib:1.1.4-3'
    compile 'com.netflix.nebula:nebula-dependency-recommender:4.1.2'

    compiler project(':dependency-script-compiler')

    testCompile localGroovy()
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }

    integTestCompile gradleTestKit()
}

gradlePlugin {
    plugins {
        dependencies {
            id = "io.ehdev.gradle.dependency-script"
            implementationClass = "io.ehdev.gradle.dependency.plugin.GradleDependencyScriptPlugin"
        }
    }
}

processResources {
    into('compiler') {
        from { configurations.compiler }
        rename 'dependency-script-compiler-.*.jar', 'dependency-script-compiler.jar'
        rename 'kotlin-compiler-embeddable-.*.jar', 'kotlin-compiler-embeddable.jar'
    }
    filteringCharset = 'UTF-8'
    filesMatching("**/*.properties") {
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: ['version': project.version as String])
    }
}

check.dependsOn integrationTest

bintray.publications = ((bintray.publications as List<String>) + ['pluginMaven', 'dependenciesPluginMarkerMaven']) as String[]
bintrayUpload.dependsOn('publishDependenciesPluginMarkerMavenPublicationToMavenLocal')
bintrayUpload.dependsOn('publishPluginMavenPublicationToMavenLocal')